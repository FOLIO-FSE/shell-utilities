#!/usr/bin/bash

read -d '' -r instructions << END
value-mapper looks for 2 column tab-delimited mapping files with the 
same names as column headers in the input file.
 
It replaces values that match the first column of the mapping file 
with the corresponding value in the second column. If no file is
found or no value in the mapping file matches the value in the input 
column, the original value is output. 

	Example:
	 
	Input file:
	col1	col2	col3
	a	b	c
	d	e	f
	g	h	i
	 
	Mapping file (named col2.tsv):
	b	x
	e	z
	 
	Output file generated:
	col1	col2	col3
	a	x	c
	d	z	f
	g	h	i


Enter the name of the tab delimited input file
END

if [[ -n "${1}" ]]; then
	infile="${1}"
	outfile="${1/.tsv/}"_fixed.tsv
else
	echo "${instructions}"
	read infile
	echo -e "\nEnter the name of the output file"
	read outfile
fi

pv "${infile}" | awk '
BEGIN {
    FS=OFS="\t"
}

NR == 1 {
	for (i = 1; i <= NF; i++) {
		header[i] = $i
		fname = $i".tsv"

 if ((system("[ -f \"" fname "\" ]") == 0)) {
			while ((getline line < fname) > 0) {
			split(line, kv, FS)
			map[fname, kv[1]] = kv[2]
			}
			close(fname)
			hasmap[i] = fname
		}
	}
	print $0
	next
}

{
	for (i = 1; i <= NF; i++) {
		if (hasmap[i]) {
			key = $i
			val = map[hasmap[i], key]
			if (val != "") {
			$i = val
			}
		}
	}
	print
}
' > "${outfile}"

echo -e "A mapped file has been sent to ${outfile}\n"
